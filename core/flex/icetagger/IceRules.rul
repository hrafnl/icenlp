STARTCLASS

BEGIN ProperNoun
IF prevToken != null ;
// hús Láru
RULE prevToken.isOnlyWordClass(NOUN) && currToken.isCase(GENITIVE) && !tag.isCase(GENITIVE) ; 
RULE prevToken.isOnlyWordClass(PREP) && !tag.caseMatch(prevToken.getTags()) ; 
// Hildar Guðmundsdóttur
RULE prevToken.isOnlyWordClass(PROPERNOUN) && prevToken.isOnlyCase(GENITIVE) && currToken.lexeme.endsWith("dóttur") && currToken.isCase(GENITIVE) && !tag.isCase(GENITIVE) ;
RULE prevToken.isOnlyWordClass(PROPERNOUN) && prevToken.isOnlyGender(FEMININE) && !tag.isGender(FEMININE) ;
RULE prevToken.isOnlyWordClass(PROPERNOUN) && prevToken.isOnlyGender(MASCULINE) && !tag.isGender(MASCULINE) ;
END ProperNoun

BEGIN Noun
IF prevToken != null ;
// hann sýnir but stakk hún hendi is ok, það sýnir aldrei
RULE (prevToken.isOnlyWordClass(PERSPRONOUN) || (prevToken.isPersonalPronoun() && prevToken.isDemonstrativePronoun()) || prevToken.lexeme.equalsIgnoreCase("við")) 
	&& currToken.isVerb() && prevToken.personNumberMatch(currToken)
	&& (prevprevToken == null || (!prevprevToken.isVerb() && !prevprevToken.isVerbInfinitive())) ; 
RULE prevToken.isOnlyWordClass(PREP) && !tag.caseMatch(prevToken.getTags()) ;
// að (cn/aa) beina (sgn/n*) mér
RULE prevToken.isInfinitive() && currToken.isVerbInfinitive() ;
// einhvern sem sæti
RULE prevToken.lexeme.equalsIgnoreCase("sem") && currToken.isVerbAny() ; 
// choose the verb
RULE prevToken.isOnlyWordClass(PROPERNOUN) && currToken.isVerb() ; 
// Disallow two nouns in a row unless the latter is in the genitive case
RULE prevToken.isOnlyWordClass(NOUN) && !prevToken.isCase(GENITIVE) && !tag.isCase(GENITIVE) 
	&& (prevprevToken == null || !prevprevToken.isProperNoun()) ;
// select the possessive pronoun, "pabbi sinn"
RULE prevToken.isOnlyWordClass(NOUN) && currToken.isPossessivePronoun() ;
RULE currToken.lexeme.equalsIgnoreCase("á") && prevToken.isAdverb() ;
// skal (ekki) hita
RULE currToken.isVerbInfinitive() && (prevToken.isVerbSpecialAuxiliary() || (prevToken.isOnlyWordClass(ADVERB) && prevprevToken!=null && prevprevToken.isVerbSpecialAuxiliary())) ;
// hafði talið, velja sagnbót
RULE currToken.isVerbSupine() && prevToken.isVerbAuxiliary() ;
// select the adjective, but ok is "fyrir framan X", and ok is "bara verð markaðarins"
RULE prevToken.isOnlyWordClass(ADVERB) && currToken.isAdjective() 
	&& (prevprevToken == null || !prevprevToken.isPreposition()) 
	&& (nextToken == null || !(nextToken.isNoun() && nextToken.isCase(GENITIVE)));
// sú líkn
RULE prevToken.isOnlyWordClass(DEMPRONOUN) && prevToken.isOnlyCase(NOMINATIVE) && !tag.isCase(NOMINATIVE) ;
RULE prevToken.isVerbBe() && !tag.isCase(NOMINATIVE) ;
// select the adjective, "hann er lasinn",
RULE prevToken.isVerbBe() && currToken.isAdjective() && prevprevToken != null && prevprevToken.isPersonalPronoun() ;

ELSE
IF prevprevToken != null ;
// Case agreement with preposition two words to the left, but only remove if a case match exists
RULE  prevprevToken.isOnlyWordClass(PREP) && !prevToken.isReflexivePronoun() 
	&& !tag.isCase(GENITIVE) && currToken.caseMatch(prevprevToken) && !tag.caseMatch(prevprevToken.getTags()) ;

ELSE
IF nextToken != null ;
// Disallow two nouns in a row unless the latter is in genitive case
// Also legal is: að minnsta kosti (þgf) herbergi (þf): Not covered!
RULE nextToken.isOnlyWordClass(NOUN) && !currToken.isOnlyWordClass(NOUN) 
	&& prevToken != null && !prevToken.isPreposition() 
	&& !nextToken.isCase(GENITIVE) && !tag.isCase(GENITIVE) ; 
// choose the adjective
RULE nextToken.isOnlyWordClass(NOUN) && !nextToken.isCase(GENITIVE) 
	&& currToken.isAdjective() && currToken.genderNumberCaseMatch(nextToken) ;
// árið (þf) 1904
RULE currToken.lexeme.equalsIgnoreCase("árið") && tag.isCase(NOMINATIVE) && nextToken.isNumeralOrdinal() ;            
RULE currToken.lexeme.equalsIgnoreCase("á") && (nextToken.isNoun() || nextToken.isAdjective()) ;
// Helps with unknown words that are possibly both a noun and a proper noun
RULE nextToken.isOnlyWordClass(PROPERNOUN) && tag.isNoun() && currToken.isProperNoun() ;
// choose the verb: gat ég
RULE nextToken.isOnlyWordClass(PERSPRONOUN) 
	&& (nextToken.isCase(NOMINATIVE) || nextToken.isCase(ACCUSATIVE)) 
	&& currToken.isVerb() ;
// o.fl.
RULE currToken.lexeme.equals("o.") && nextToken.lexeme.equals("fl.") ;
END Noun


BEGIN Adj
IF prevToken != null ;
// hann sýnir.  Same condition as for nouns
RULE (prevToken.isOnlyWordClass(NOUN) || prevToken.isOnlyWordClass(PERSPRONOUN) || (prevToken.isPersonalPronoun() && prevToken.isDemonstrativePronoun()))
	&& currToken.isVerb() && !currToken.isVerbPastPart() 
	&& (prevprevToken == null || (!prevprevToken.isVerb() && !prevprevToken.isVerbInfinitive())) ;
// að (cn/aa) beina (sgn/l*) mér 
RULE prevToken.isInfinitive() && currToken.isVerbInfinitive() ;
// niður í tær (select the noun)
RULE prevToken.isOnlyWordClass(PREP) && !tag.caseMatch(prevToken.getTags()) ;
// miklu meira, bjartur depill (unknown)
RULE prevToken.isOnlyWordClass(ADJ) 
	&& (currToken.isAdverb() || (currToken.isNoun() && currToken.genderNumberCaseMatch(prevToken))) ;
// þessi dýr 
RULE prevToken.isOnlyWordClass(DEMPRONOUN) && currToken.isNoun() 
	&& (nextToken == null || !nextToken.isNoun())  ;
// choose the verb    tunglið óð í skýjum
RULE prevToken.isOnlyWordClass(NOUN) && currToken.isVerb() 
	&& nextToken != null && nextToken.isOnlyWordClass(PREP) ;
// Remove strong declension if weak exists and article preceeds
RULE tag.isAdjectiveStrong() && currToken.isAdjectiveWeak() &&  prevToken.isOnlyWordClass(ARTICLE) ;	

ELSE
IF nextToken != null ;
RULE nextToken.isOnlyWordClass(PERSPRONOUN) && tag.isAdjectivePositive() ;
// select the noun
RULE nextToken.isOnlyWordClass(ADJ) && currToken.isNoun() ;
// select the adverb
RULE nextToken.isOnlyWordClass(ADJ) && currToken.isAdverb() ;
// if the next is a noun and the current is an adverb than remove the adjective if not a match with the noun
RULE nextToken.isOnlyWordClass(NOUN) && currToken.isAdverb() 
	&& !nextToken.isOnlyCase(GENITIVE) && !currToken.genderNumberCaseMatch(nextToken) ;
// fyrst hún borðaði, fyrst þeir fóru
RULE currToken.lexeme.equalsIgnoreCase("fyrst") && (nextToken.isNoun() || nextToken.isProperNoun() || nextToken.isPersonalPronoun()) 
	&& nextnextToken != null && nextnextToken.isVerbActive() ; 
// choose the supine, "bætt um betur"
RULE currToken.isVerbSupine() && nextToken.isOnlyWordClass(ADVERB) ;
// choose the adverb
RULE currToken.lexeme.endsWith("lega") && currToken.isAdverb() && !nextToken.isNoun() ;
// eins og
RULE currToken.lexeme.equalsIgnoreCase("eins") && nextToken.lexeme.equalsIgnoreCase("og") ;
// margra landa
RULE tag.isAdjectiveComparative() && currToken.isAdjectivePositive() && nextToken.isNoun() && !tag.genderNumberCaseMatch(nextToken.getTags()) ;
// í eitt ár  HL: 13.03.2010
RULE currToken.isNumeral() && nextToken.lexeme.equalsIgnoreCase("ár") ;
                   	
END Adj


BEGIN Adverb
IF prevToken != null ;
RULE prevToken.isOnlyWordClass(INF) ;
RULE prevToken.isOnlyWordClass(POSSPRONOUN) ;
RULE prevToken.isOnlyWordClass(ARTICLE) ;
 // "Jón heldur að" but ok is "ekki Jón heldur"
RULE currToken.lexeme.equals("heldur") && (prevprevToken == null || !prevprevToken.lexeme.equalsIgnoreCase("ekki")) 
	&& (prevToken.isOnlyWordClass(NOUN) || prevToken.isOnlyWordClass(PROPERNOUN) || prevToken.isOnlyWordClass(PERSPRONOUN))
        && prevToken.personNumberMatch(currToken) ;
// þeim stundum
RULE prevToken.isOnlyWordClass(DEMPRONOUN) && currToken.lexeme.equals("stundum") ; 
// Tárin sem
RULE (prevToken.isOnlyWordClass(NOUN) || prevToken.isOnlyWordClass(PROPERNOUN)) && currToken.lexeme.equalsIgnoreCase("sem") ;
// Choose the adjective : "nægilega sterkt skip"
RULE prevToken.isOnlyWordClass(ADVERB) && currToken.isAdjective() 
	&& nextToken != null && nextToken.isNoun() && currToken.genderNumberCaseMatch(nextToken) ;
// Choose the adjective: "atriði eru eins."
RULE currToken.isAdjective() && prevToken.isVerbBe() && nextToken != null && nextToken.isPunctuation() ;

ELSE 
IF nextToken != null && !nextToken.lexeme.equalsIgnoreCase("á") ;
// vissi að hún ...
RULE currToken.lexeme.equalsIgnoreCase("að") && (nextToken.isVerbInfinitive() || nextToken.isNoun() || nextToken.isPersonalPronoun()) ;
// við erum
RULE currToken.lexeme.equalsIgnoreCase("við") && nextToken.isVerbBe() ; 
// stjórna því sem..
RULE currToken.lexeme.equalsIgnoreCase("því") && nextToken.isRelativeConjunction() ; 
// fyrst þeir fóru
RULE currToken.lexeme.equalsIgnoreCase("fyrst") 
	&& (nextToken.isNoun() || nextToken.isProperNoun() || nextToken.isPersonalPronoun()) 
	&& nextnextToken != null && nextnextToken.isVerbActive() ; 
RULE nextToken.isOnlyWordClass(CONJREL) ;
RULE nextToken.isOnlyWordClass(REFLPRONOUN) ;
// choose the adjective, if a match exists
RULE (currToken.isAdjective() || currToken.isIndefinitePronoun()) 
	&& (nextToken.isOnlyWordClass(NOUN) || nextToken.isOnlyWordClass(PROPERNOUN)) && currToken.genderNumberCaseMatch(nextToken) ;
// hægt er, hægt að sinna
RULE currToken.isAdjective() && (currToken.lexeme.equalsIgnoreCase("hægt") || currToken.lexeme.equalsIgnoreCase("rétt")) 
	&& (nextToken.isVerbBe() || nextToken.isOnlyWordClass(INF)) ;
// Choose the noun
RULE currToken.isNoun() && prevToken != null && prevToken.isNumeral() ;	
// Remove if the current is also preposition - "Um leið ...", but ok is "fyrir utan (aa_ae) gluggann"
RULE currToken.isPreposition() && (prevToken == null || !prevToken.isPreposition())  
	&& !nextToken.lexeme.equals("við") && nextToken.isNominal() && currToken.caseMatch(nextToken) ;

ELSE
IF prevToken != null && nextToken != null ;
// mynd SEM túlkaði
RULE prevToken.isNoun() && nextToken.isOnlyVerbAny() ;
END Adverb


BEGIN PersonalPronoun
IF nextToken != null ;
// þá auðveldustu
// select the demonstrative pronoun
RULE  nextToken.isOnlyAdjectiveSuper() ;
RULE currToken.lexeme.equalsIgnoreCase("þá") 
	&& (	(nextToken.isVerb() && !nextToken.isVerbCaseMarking()) ||
                nextToken.isConjunction() || nextToken.isAdverb() || 
                nextToken.isNominal()
           ) ;
// stjórna því sem..
RULE currToken.lexeme.equalsIgnoreCase("því") 
	&& (nextToken.isVerbPastPart() || nextToken.isRelativeConjunction()) ; 
// select the demonstrative , "þeir sem", "þeir, sem"
RULE (currToken.lexeme.equals("þeir") || currToken.lexeme.equals("þær") || currToken.lexeme.equals("þau") ||
      currToken.lexeme.equalsIgnoreCase("það") || currToken.lexeme.equalsIgnoreCase("þess")) &&
      (nextToken.lexeme.equalsIgnoreCase("sem") ||
      (nextToken.lexeme.equals(",") && nextnextToken != null && nextnextToken.lexeme.equalsIgnoreCase("sem"))) ;
// við kirkjugarðinn
// "Við Helgi fórum" is ok  , "hann pabbi" is ok, "til mín augunum" is ok, "komum við síðdegis", is ok
RULE currToken.lexeme.equalsIgnoreCase("við") 
	&& ((nextToken.isNominal() && currToken.caseMatch(nextToken) && !nextToken.isCase(NOMINATIVE) 
	&& !nextToken.isVerbAny() 
	&&  (prevToken==null || !(prevToken.isVerb() && currToken.personNumberMatch(prevToken))))   // "vorum við" is ok
          || 
        (nextToken.isPunctuation() && prevToken != null && !currToken.personNumberMatch(prevToken)) // á bak við?
           ) ;
// í því skyni
// Important: Only assume demonstrative pronoun if there is a match with the following noun
RULE  nextToken.isOnlyWordClass(NOUN) 
	&& currToken.isDemonstrativePronoun() && currToken.genderNumberCaseMatch(nextToken) ;

ELSE
IF prevToken != null ;
// snúa sér við, bætti hún við
// sneri blaðinu við, hafði rangt við, blasti tóm flaskan við
RULE currToken.lexeme.equalsIgnoreCase("við") 
    && (
           (prevprevToken!=null && (prevprevToken.isVerb() || prevprevToken.isVerbInfinitive() || prevprevToken.isAdjective()) 
		&&  (prevToken.isNoun() || prevToken.isPersonalPronoun() || prevToken.isReflexivePronoun())
           ) ||
    	prevToken.isOnlyWordClass(VERBINF) || // snúa við
    	(prevToken.isOnlyVerbAny() && (
    	prevToken.isVerbSupine() ||                    // haldið við
        prevToken.isVerbPastPart() ||                    // var bætt við
        prevToken.isVerbMiddleForm() ||                   // ræðst við
        (prevprevToken != null && prevprevToken.lexeme.equalsIgnoreCase("við")) )) // við gengum við
    ) ;
// Choose the adverb
RULE currToken.lexeme.equalsIgnoreCase("því") && currToken.isAdverb() 
	&& (prevToken.isVerbBe() || prevToken.isVerbMiddleForm()) ; // er því, gefst því
// væri þá and réði sig þá
RULE currToken.lexeme.equalsIgnoreCase("þá") 
	&& (prevToken.isVerbBe() || prevToken.isOnlyWordClass(REFLPRONOUN) || prevToken.isPersonalPronoun()) ;  // finnst þér þá
// Er mamma þín við?
RULE prevToken.isOnlyWordClass(POSSPRONOUN) ;
END PersonalPronoun

BEGIN IndefinitePronoun
IF nextToken != null ;
RULE (nextToken.isOnlyWordClass(PERSPRONOUN) || (nextToken.isPersonalPronoun() && nextToken.isDemonstrativePronoun())) 
	&& !nextToken.isCase(GENITIVE) ;
RULE currToken.lexeme.equalsIgnoreCase("hvort") && !nextToken.isPreposition() 
	&& !nextToken.lexeme.equalsIgnoreCase("annað") && !nextToken.isCase(GENITIVE) ; // í lagi er: hvort tveggja, hvort annað
// "bæði (c) Jón og Gunna", but "bæði svínin" is ok
RULE currToken.lexeme.equalsIgnoreCase("bæði") && !currToken.genderNumberCaseMatch(nextToken) ;

ELSE
IF prevToken != null ;
RULE prevToken.isPreposition() && !tag.caseMatch(prevToken.getTags()) ;
                
END IndefinitePronoun


BEGIN DemonstrativePronoun
IF prevToken != null ;
// sá hinn sami
RULE prevToken.isOnlyWordClass(DEMPRONOUN) ;
RULE prevToken.isPreposition() && !tag.caseMatch(prevToken.getTags()) ;
RULE currToken.lexeme.equalsIgnoreCase("þá") 
	&& (prevToken.isVerbBe() || prevToken.isOnlyWordClass(REFLPRONOUN) || prevToken.isOnlyWordClass(PERSPRONOUN));
// pabbi sá auglýsingu
RULE currToken.lexeme.equals("sá") && prevToken.isOnlyWordClass(NOUN) && nextToken != null && !nextToken.isPunctuation() ;

ELSE
IF nextToken != null ;
// hlýtur þá að vera
RULE nextToken.isOnlyWordClass(INF) ;
RULE nextToken.isOnlyWordClass(DEMPRONOUN) ;
// hinn (article) þreytti maður
RULE currToken.isArticle() && nextToken.isOnlyWordClass(ADJ) ;
RULE nextToken.isVerbBe() && currToken.isPersonalPronoun() ;
RULE currToken.lexeme.equalsIgnoreCase("þá") 
	&& ( (nextToken.isVerb() && !nextToken.isRelativeConjunction()) ||    // þá sem ...
             ((nextToken.isConjunction() || nextToken.isAdverb()) && prevToken == null) || // Þá þegar
             ((nextToken.isOnlyWordClass(NOUN) || nextToken.isOnlyWordClass(PROPERNOUN)) &&
                        prevToken != null && prevToken.isOnlyVerbAny())) ;   // gekk þá maðurinn
RULE currToken.lexeme.equalsIgnoreCase("því") && nextToken.isVerbPastPart() ;
END DemonstrativePronoun

BEGIN ReflexivePronoun
IF nextToken != null ;
// "í sjálfum mér" is ok
RULE nextToken.isOnlyWordClass(PERSPRONOUN) 
	&& !(nextToken.lexeme.equalsIgnoreCase("mér") || nextToken.lexeme.equalsIgnoreCase("þér")) ;
ELSE
IF prevToken != null ;
RULE currToken.lexeme.equalsIgnoreCase("sér") 
	&& (prevToken.isOnlyWordClass(PERSPRONOUN) || prevToken.isOnlyWordClass(NOUN) ||
                        prevToken.isOnlyWordClass(PROPERNOUN)) ;
END ReflexivePronoun

BEGIN InterrogativePronoun
IF nextToken != null ;
RULE nextToken.isIndefinitePronoun() ;
// í lagi er: hvort er
RULE currToken.lexeme.equalsIgnoreCase("hvort") && !nextToken.isVerb() ;  
RULE currToken.lexeme.equalsIgnoreCase("hvers") && nextToken.isNoun() ;   // hvers kyns: hér er hvers óáfn.
// hver einasti
RULE currToken.lexeme.equalsIgnoreCase("hver") && (nextToken.isOnlyWordClass(ADJ) || nextToken.isOnlyWordClass(NOUN)) ;

ELSE
IF prevToken != null ;
RULE prevToken.isOnlyWordClass(PERSPRONOUN) ;
// "í hverju orði", "á hverju götuhorni" but "í hvaða formi" is ok
RULE !currToken.lexeme.equals("hvaða") && nextToken != null && prevToken.isPreposition() && nextToken.isOnlyWordClass(NOUN) ;

END InterrogativePronoun


BEGIN PossessivePronoun
IF prevToken != null ;
// í fyrsta sinn
RULE prevToken.isAdjectiveSuper() ;   
RULE prevToken.isDemonstrativePronoun() || prevToken.isIndefinitePronoun() || prevToken.isInfinitive() ;
END PossessivePronoun

BEGIN Preposition
IFEXIT currToken.lexeme.equalsIgnoreCase("vegna") ;
RULE (nextToken != null && !(nextToken.isNominal() && nextToken.isCase(GENITIVE)) 
	&& (prevToken == null ||  !(prevToken.isNominal() && prevToken.isCase(GENITIVE)))) ||
           (nextToken == null && prevToken != null && !(prevToken.isNominal() && prevToken.isCase(GENITIVE)) ;
ELSE
IF currToken.lexeme.equalsIgnoreCase("að") ;
RULE prevToken != null 
	&& (prevToken.lexeme.equals("til") || prevToken.lexeme.equals("því") || prevToken.lexeme.equals("um")) ;
// til þess að
RULE prevToken != null && prevprevToken != null && prevprevToken.lexeme.equalsIgnoreCase("til") && prevToken.lexeme.equals("þess") ;
RULE nextToken != null 
	&& (nextToken.isVerbInfinitive() || nextToken.isOnlyWordClass(ADVERB)) ;
ELSE
// á móti mér
RULE currToken.lexeme.equals("á") && 
 (
	(nextToken != null && nextToken.lexeme.equals("móti") && 
	 nextnextToken != null && nextnextToken.isNominal() && nextToken.caseMatch(nextnextToken)) ||        
// Hver á ...
	(prevToken != null && prevToken.isInterrogativePronoun() && 
	(prevToken.lexeme.equalsIgnoreCase("hver") || prevToken.lexeme.equalsIgnoreCase("hvað"))) ||
// Ég á
	(prevprevToken == null && prevToken != null && prevToken.isPersonalPronoun()) ||
// Ég/Maður á (ekki) að
	(prevToken != null && (prevToken.isOnlyWordClass(PERSPRONOUN) || prevToken.isOnlyWordClass(NOUN) || prevToken.isProperNoun()) &&
	prevToken.personNumberMatch(currToken) && prevToken.isCase(NOMINATIVE) &&
	nextToken != null && (nextToken.isOnlyWordClass(INF) || nextToken.lexeme.equals("ekki") || nextToken.lexeme.equals("von")))
 );


ELSE
IF prevToken != null ;
RULE currToken.lexeme.equalsIgnoreCase("við") &&  prevToken.isVerbAny() && currToken.personNumberMatch(prevToken) ;

ELSE
IF nextToken != null ;// innan undir er=sfg3en<v>_sfg1en<v>_ct_c
RULE nextToken.lexeme.equalsIgnoreCase("er") ;
RULE nextToken.isOnlyVerbAny() ;
RULE nextToken.isRelativeConjunction() ;
RULE nextToken.isOnlyWordClass(INF) && currToken.lexeme.equals("eftir") ;
// "með að", "eftir að"
RULE (currToken.lexeme.equals("með") || currToken.lexeme.equals("eftir")) && nextToken.lexeme.equals("að") && tag.isCase(DATIVE);
// "blá í framan af kulda",  langt í burtu
RULE nextToken.isOnlyWordClass(ADVERB) && nextnextToken != null 
	&& (nextnextToken.isPreposition() || nextnextToken.isOnlyWordClass(ADVERB) ||
            nextnextToken.isConjunction() || nextnextToken.isPunctuation() ||
            // að svo fjölmenn ...
            ( (nextnextToken.isOnlyWordClass(ADJ) ||
               nextnextToken.isOnlyWordClass(NOUN) ||  // hefur í senn lífsviðurværi
               nextnextToken.isOnlyWordClass(NUMERAL) ||
               nextnextToken.isOnlyWordClass(PROPERNOUN) ||
               nextnextToken.isOnlyWordClass(PERSPRONOUN))  // fyrir aftan hana
              && !tag.caseMatch(nextnextToken.getTags()))) ;
RULE ((nextToken.isAdjective() && !(nextToken.isAdverb() && nextnextToken != null && nextnextToken.isAdjective()) && !nextToken.isOnlyCase(GENITIVE)) ||  // með helsti kynlegu ...
          nextToken.isOnlyWordClass(DEMPRONOUN) ||
          nextToken.isArticle()  ||
          (nextToken.isOnlyWordClass(NUMERAL) && !nextToken.isNumeralOrdinal() &&
                    !nextToken.isOnlyCase(GENITIVE))) 
         && !tag.caseMatch(nextToken.getTags())  ;
// Ok is "viss um , að" or "að hugsa um."
RULE nextToken.isPunctuation() && !currToken.lexeme.equals("um")
	&& (nextnextToken==null || !nextnextToken.lexeme.equalsIgnoreCase("að")) 
	&& !(nextToken.lexeme.equals("«") && nextnextToken != null && tag.caseMatch(nextnextToken.getTags())) ;
// "hugsa um (ao) að (c)" er í lagi, "eftir að" er í lagi
RULE nextToken.isOnlyWordClass(CONJ) && !currToken.lexeme.equals("um") 
	&& !(currToken.lexeme.equals("eftir") && tag.isCase(ACCUSATIVE) && nextToken.lexeme.equals("að")) ;
// "Eigum við ekki"
RULE currToken.lexeme.equalsIgnoreCase("við") && nextToken.lexeme.equalsIgnoreCase("ekki") ;
// "ok is til að hjálpa" , ok is "fyrir utan(aa_ae) gluggann"
RULE nextToken.isPreposition() && !nextTokenFirstTag.isAdverb() && !nextToken.isInfinitive() && !nextToken.isNoun() ; 
// um 1903, 
RULE currToken.lexeme.equals("um") && nextToken.isNumeral() ;
// flest sex til sjö
RULE prevToken != null && currToken.lexeme.equals("til") && prevToken.isOnlyWordClass(NUMERAL) && nextToken.isOnlyWordClass(NUMERAL) ;                 
// Check pronouns/nouns/proper nouns:
RULE (nextToken.isPronoun() || nextToken.isNoun() || nextToken.isProperNoun())
     && !tag.caseMatch(nextToken.getTags()) 
     && !((currToken.isAdverb() &&
           (nextToken.isProperNoun() || (nextToken.isOnlyWordClass(NOUN) && (prevToken==null || prevToken.isNoun() || prevToken.isVerb())))
          ) ||
          // "á hans aldri" / "eftir andartaks þögn", "í orðsins fyllstu merkingu", "fyrir neðan hana" is ok
          // also með tveggja tíma viðdvöl , NOTE: proper nouns might have missing tags
             (nextToken.isCase(GENITIVE) && nextnextToken != null &&
             (((nextnextToken.isOnlyWordClass(NOUN) || nextnextToken.isOnlyWordClass(ADJ) ||
                nextnextToken.isOnlyWordClass(PROPERNOUN) 
             )
            && tag.caseMatch(nextnextToken.getTags())) || nextnextToken.isCase(GENITIVE))) 
         ) ;
END Preposition

BEGIN Infinitive
IF nextToken != null ;
RULE !nextToken.isVerbInf() || !nextToken.isVerbAny() ;
END Infinitive

BEGIN VerbInfinitive
IF prevToken != null ;
RULE prevToken.isOnlyWordClass(REFLPRONOUN) && prevToken.isCase(NOMINATIVE) ;
RULE prevToken.isVerbInfinitive() && !prevToken.isVerbSpecialAuxiliary() ; // láta smyrja is ok
RULE prevToken.isOnlyWordClass(PREP) ;
RULE prevToken.isRelativeConjunction() && (nextToken==null || !nextToken.isVerbSpecialAuxiliary()) ;   // konurnar tvær sem FÁ, but ok is "sem rekja má.."
RULE prevToken.isVerbAuxiliary() ;   // hefði smitast
// hin syrgða
RULE prevToken.isArticle() ;
// þessi kæfa
RULE prevToken.isOnlyWordClass(DEMPRONOUN) ;
// choose the adjective : í sinn RÉTTA tíma
RULE prevToken.isNominal() && prevprevToken != null && prevprevToken.isPreposition() 
	&& currToken.isAdjective() && nextToken != null && nextToken.isNoun() ;
// margra landa
RULE prevToken.isOnlyWordClass(ADJ) && prevToken.isOnlyCase(GENITIVE) && 
	currToken.isNoun() && currToken.isCase(GENITIVE) && currToken.genderNumberCaseMatch(prevToken);
	
ELSE
IF prevprevToken != null ;
// til fjarlægra landa
RULE prevprevToken.isOnlyWordClass(PREP) && prevToken.isOnlyWordClass(ADJ) ;

ELSE
IF nextToken != null;
// kvíða sínum
RULE currToken.isNoun() & nextToken.isOnlyWordClass(POSSPRONOUN);

ELSE
IF prevToken == null;
RULE currToken.isNoun() && nextToken != null && nextToken.isVerb() ; // A noun is more likely as the first word
END VerbInfinitive

BEGIN Verb
IF prevToken == null ;
RULE currToken.lexeme.equalsIgnoreCase("er") && nextToken != null && nextToken.isVerbInfinitive() ; // Er hausta tekur
RULE currToken.isNoun() && nextToken != null && nextToken.isVerb() ; // A noun is more likely as the first word

ELSE
IF prevToken != null ;
RULE prevToken.isOnlyWordClass(PREP) && !prevToken.lexeme.equalsIgnoreCase("vegna") ;
RULE prevToken.isOnlyWordClass(ARTICLE) ;
RULE prevToken.isInfinitive() && currToken.isVerbInfinitive() ;    // Infinitive verb are handled above
// á RÉTTI dagsins but make sure that "við höfðum ekkert".. works
RULE prevToken.isPreposition() && !prevToken.isPersonalPronoun() && currToken.isNoun() ;
 // "var orðið",
RULE (prevToken.isVerbBe() && !prevToken.isVerbSupine() && !prevToken.isVerbPastPart()) && (tag.isVerbSupine() || tag.isVerbIndicativeForm() || tag.isVerbSubjunctiveForm()) ;
// tók lokið af
RULE tag.isVerbPastParticiple() 
	&& !prevToken.lexeme.equalsIgnoreCase("sem") && (prevToken.isVerb() || prevToken.isVerbInfinitive()) 
	&& !prevToken.isVerbBe() && (nextToken == null || !nextToken.isVerbBe()) ;
// HL: 13.03.2010 Þú GERÐIR það sjálfur
RULE tag.isVerbPastParticiple() && currToken.lexeme.equalsIgnoreCase("gerðir") && prevToken.isPersonalPronoun() ;
RULE prevToken.isVerbSpecialAuxiliary() ;
// skal (ekki) hita
RULE currToken.isVerbInfinitive() 
	&& (prevToken.isVerbSpecialAuxiliary() || (prevToken.isOnlyWordClass(ADVERB) && prevprevToken != null && prevprevToken.isVerbSpecialAuxiliary())) ;
// hefði (mikið) MÓTAST, select the supine
RULE (tag.isVerbIndicativeForm() || tag.isVerbSubjunctiveForm()) && currToken.isVerbSupine() 
	&& ( (prevToken != null && prevToken.isVerbAuxiliary()) || (prevprevToken != null && prevprevToken.isVerbAuxiliary())) ;
// hin eina sanna
RULE prevToken.isAdjective() && prevprevToken != null && (prevprevToken.isOnlyWordClass(ARTICLE)) ;
// select the noun "þróttmiklum söng"
RULE prevToken.isOnlyWordClass(ADJ) && currToken.isNoun() && currToken.genderNumberCaseMatch(prevToken) ;
// ört vaxandi umferð
RULE prevToken.isOnlyWordClass(ADVERB) && currToken.isAdjective() && nextToken != null 
	&& nextToken.isOnlyWordClass(NOUN) && currToken.genderNumberCaseMatch(nextToken) ;
// maðurinn sem
RULE currToken.isRelativeConjunction() && currToken.lexeme.equalsIgnoreCase("sem") 
	&& (prevToken.isOnlyWordClass(NOUN) || prevToken.isOnlyWordClass(PROPERNOUN)) ;
// subjunctive  , "ef hún gætti sín ekki"
RULE prevprevToken != null && (prevprevToken.lexeme.equalsIgnoreCase("ef") || prevprevToken.lexeme.equalsIgnoreCase("þótt")) 
	&& currToken.isVerbSubjunctive() && !tag.isVerbSubjunctiveForm() ;
// mín gæfa , select the noun
RULE prevToken.isPossessivePronoun() && currToken.isNoun() ;
// á hennar pökkum, í þetta skipti
RULE (prevToken.isPersonalPronoun() || prevToken.isPossessivePronoun() || prevToken.isDemonstrativePronoun()) 
	&& prevprevToken != null && prevprevToken.isPreposition() && currToken.isNoun() ;
// ýmsir SKÆÐIR keppinautar, select the adjective
RULE prevToken.isIndefinitePronoun() && currToken.isAdjective() 
	&& nextToken != null && nextToken.isNoun() && !tag.personGenderNumberMatch(prevToken.getTags()) ;
RULE prevToken.isOnlyWordClass(DEMPRONOUN) && currToken.isNoun() ;
RULE currToken.lexeme.equalsIgnoreCase("sem") && !prevToken.isPersonalPronoun() ;

ELSE
IF nextToken != null ;
RULE currToken.lexeme.equalsIgnoreCase("mun") && nextToken.isAdjectiveComparative() ; // mun fleiri
RULE currToken.lexeme.equalsIgnoreCase("eigi") && nextToken.isOnlyVerbAny()  ; // eigi skal höggva
// sá sem, sá skal, karlinn sá, sá maður
RULE currToken.lexeme.equalsIgnoreCase("sá") && (nextToken.isRelativeConjunction() || nextToken.isOnlyVerbAny() ||  
    nextToken.isEOS() || (nextToken.isNoun() && nextToken.isOnlyCase(NOMINATIVE))) ; 
 // er gerðist
RULE currToken.lexeme.equalsIgnoreCase("er") 
	&& nextToken.isOnlyVerbAny() && !nextToken.isVerbPastPart() 
	&& (nextToken.isVerbMiddleForm() || nextToken.isVerbIndicative() || nextToken.isVerbSubjunctive())  ;
// "... á meðan "...
RULE currToken.lexeme.equalsIgnoreCase("á") 
	&& (((nextToken.isConjunction() && !nextToken.isInfinitive()) || nextToken.isPunctuation()) ||
           // á þessum
           (nextToken.isNominal() && nextToken.isOnlyCase(DATIVE)) ||
           (nextToken.lexeme.equals("milli") && (prevToken == null || (!prevToken.isProperNoun() && !prevToken.isPersonalPronoun()))) ||
           // "hingað á flugvöllinn", but ok is "sem á .."
           (prevToken != null && !prevToken.isCase(NOMINATIVE) && !prevToken.isRelativeConjunction())
           ) ;
// heldur luralegur
RULE nextToken.isOnlyWordClass(ADJ) && currToken.isAdverb() && currToken.lexeme.equalsIgnoreCase("heldur") ;
// gengi hlutabréfa
RULE nextToken.isOnlyWordClass(NOUN) && nextToken.isOnlyCase(GENITIVE) && currToken.isNoun() ;
// select the adjective; "sagði skjálfandi röddu"
RULE nextToken.isNoun() && tag.isVerbPresentPart() && currToken.isAdjective() ;
// select the adjective; "nýbökuðum (unknown) stjórnarformanni"
RULE nextToken.isOnlyWordClass(NOUN) && currToken.isAdjective() 
	&& currToken.genderNumberCaseMatch(nextToken) && nextToken.isOnlyCase(DATIVE) ;
 // engin LEIÐ var, select the noun, ok is "hverjir GETA verið ..."
RULE nextToken.isVerbBe() && !nextToken.isVerbSupine() 
	&& prevToken != null && prevToken.isIndefinitePronoun() && currToken.isNoun() ;
 // nefna má..
RULE currToken.isVerbInfinitive() && nextToken.isVerbSpecialAuxiliary() ;
RULE currToken.isNoun() & nextToken.isOnlyWordClass(POSSPRONOUN);
END Verb

BEGIN RelativeConjunction
IF currToken != null ;
RULE prevToken == null ;
RULE prevToken.lexeme.equalsIgnoreCase("þar") ;   // þar sem
RULE prevToken.isVerbMiddleForm() ; // virðist sem
RULE prevToken.isOnlyWordClass(ADVERB)  ; // enn sem ..
RULE (currToken.lexeme.equals("sem") 
	&& prevToken.isAdjectiveComparative() && prevprevToken != null && prevprevToken.lexeme.equals("því")) ; // því betri sem
// ok is "gæti verið sem var", "að finna sem hann"
RULE prevToken.isOnlyVerbAny() 
	&& (nextToken == null || (!nextToken.isVerbIndicative() && !nextToken.isVerbSubjunctive())) ;
RULE currToken.lexeme.equalsIgnoreCase("er") 
	&& ((prevToken != null && prevToken.isOnlyWordClass(CONJREL)) ||
            (nextToken != null && nextToken.isOnlyWordClass(INF))) ; // fólk er að ...
// sem betur fer, sem slíkur
RULE nextToken != null 
	&& (nextToken.isOnlyWordClass(REFLPRONOUN) || nextToken.lexeme.equalsIgnoreCase("betur")) ;
// enda sem grenjuskjóða.
RULE nextToken != null && (nextToken.isOnlyWordClass(NOUN) || nextToken.isReflexivePronoun()) 
	&& nextnextToken != null && nextnextToken.isEOS() ;
                    
END RelativeConjunction


BEGIN Conjunction
IF currToken.lexeme.equalsIgnoreCase("sem") ;
// svo sem
RULE  (prevToken != null && (prevToken.isNoun() || prevToken.lexeme.equalsIgnoreCase("svo"))) ||
	(nextToken != null && (nextToken.isVerbBe() || nextToken.isAdverbSuper() || nextToken.isAdjectiveSuper())) ; // sem fyrst, sem best
ELSE
IF currToken.lexeme.equalsIgnoreCase("utan") ;
RULE nextToken != null && nextToken.isPreposition() ; // utan í
RULE prevToken != null && prevToken.lexeme.equalsIgnoreCase("fyrir") ; // fyrir utan

//ELSE
//IF currToken.lexeme.equalsIgnoreCase("nema") ;
//RULE prevToken != null && prevToken.isOnlyWordClass(PREP) ; // kominn með nema

ELSE
IF currToken.lexeme.equalsIgnoreCase("að") ;
// First there are some special cases where "að" is correctly a conjunction
// ég sagði (mér) að...  , það var nefnt að..
RULE  (prevToken == null || 
	( !prevToken.isVerbSpecialInf() &&  
	  prevToken.tokenCode != COMMA &&  // , að
      	  !prevToken.lexeme.equals("svo") &&  // svo að ..
      // held að Kalli..., beindust að is ok
          (!prevToken.isVerb() || prevToken.isVerbMiddleForm() 
      		|| nextToken == null || (!nextToken.isNoun() && !nextToken.isProperNoun()) || !nextToken.isCase(NOMINATIVE))
	)) &&      
      // Here reduction rules start
      (
	 nextToken != null && (
         // "Hver heldur þú að finni" is ok,  "að enda"
         ((nextToken.isOnlyWordClass(VERB) && !nextToken.isVerbSubjunctive()) ||
           nextToken.isVerbInfinitive() ||
           (nextToken.isAdverb() && nextnextToken != null && nextnextToken.isPunctuation()) ) || // að aftan .
           (nextToken.isVerbInfinitive() && nextToken.isOnlyVerbAny()) ||   // að lesa
           (nextToken.isNominal()  && !nextToken.lexeme.equals("á") 
           	&& !nextToken.lexeme.equals("við") && currToken.caseMatch(nextToken)) || // could be a prep "aþ"
           (nextToken.isCase(GENITIVE) && nextnextToken != null &&
                ((nextnextToken.isOnlyWordClass(NOUN) || nextnextToken.isOnlyWordClass(ADJ) ||
                  nextnextToken.isOnlyWordClass(PROPERNOUN))
             && currToken.caseMatch(nextnextToken)))
	 )
      ) ;

ELSE
IF nextToken != null ;
// var þegar dáinn
RULE currToken.lexeme.equalsIgnoreCase("þegar") && (nextToken.isVerbPastPart()) && prevToken!=null && prevToken.isVerbBe() ;  
// heldur en (heldur er hér aam)
RULE currToken.lexeme.equalsIgnoreCase("heldur") && (nextToken.lexeme.equalsIgnoreCase("en") || nextToken.lexeme.equalsIgnoreCase("áfram")) ;
RULE nextToken.isPunctuation() ;

ELSE
IF prevToken != null ;
RULE prevToken.isOnlyWordClass(CONJREL) ; // sem og , sem er
RULE prevToken.isOnlyWordClass(INF) ;
// var heldur ekki, og heldur fast
RULE (prevToken.isVerbBe() || prevToken.isConjunction()) && currToken.lexeme.equalsIgnoreCase("heldur") ;

END Conjunction

BEGIN Numeral
IF nextToken != null ;
RULE !nextToken.isNoun() && !nextToken.isAdjective() && !nextToken.isPunctuation() 
	&& !nextToken.isConjunction()        // tvö og þrú saman
        && !nextToken.isPreposition()        // sátu þrjú í ...
        && !nextToken.isAdverb() && !nextToken.isVerb() ;
RULE (currToken.lexeme.equalsIgnoreCase("einn") || currToken.lexeme.equalsIgnoreCase("ein")) 
	&& currToken.isAdjective() && !nextToken.isNominal() ;

ELSE
IF prevToken != null ;
// "hin eina sanna"
RULE prevToken.isArticle() ;
                 
END Numeral

BEGIN Exclamation
IF nextToken != null ;
RULE nextToken.isAdverb() || nextToken.isAdjective() ;
END Exclamation

BEGIN Article
IF nextToken != null ;
RULE !nextToken.isAdjective() && !nextToken.isReflexivePronoun() ;  // hið sama
END Article

ENDCLASS
